اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/سندات"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛

استخدم مـتم؛
استخدم مـؤجلات؛

// تتمة:
صنف رقـم {
    عرف القيمة: صـحيح؛
    عملية هذا~هيئ() هذا.القيمة = 0؛
    عملية هذا~هيئ(i: صـحيح) هذا.القيمة = i؛
    عملية هذا~هيئ(i: سند[رقـم]) هذا.القيمة = i.القيمة؛
    عملية هذا = سند[رقـم] هذا.القيمة = value.القيمة؛
}

// اختبر مـؤجلة.
دالة اختبر_مؤجلة {
    طـرفية.اطبع("اختبر مـؤجلة:\ج")؛
    عرف مؤجلة: سـندنا[مـؤجلة[رقـم]] = مـؤجلة[رقـم].أنشئ()؛
    طـرفية.اطبع("مؤجلة 1 - الحالة: %d، النتيجة: %d\ج"، مؤجلة.الحالة، مؤجلة.النتيجة.القيمة)؛
    مؤجلة.قرر(رقـم(5))؛
    طـرفية.اطبع("مؤجلة 1 - الحالة: %d، النتيجة: %d\ج"، مؤجلة.الحالة، مؤجلة.النتيجة.القيمة)؛
    مؤجلة.ارفض(مثل_سندنا[سـندنا[خـطأ_عام]().{
        أنشئ()؛
        الرمز = 1؛
        الرسالة = نـص("خطأ غير معروف 1")؛
    }، خـطأ])؛
    طـرفية.اطبع("مؤجلة 1 - الحالة: %d، الخطأ: %ld\ج"، مؤجلة.الحالة، مؤجلة.الخطأ.obj~ptr)؛

    مؤجلة = مـؤجلة[رقـم].أنشئ()؛
    طـرفية.اطبع("مؤجلة 2 - الحالة: %d، النتيجة: %d\ج"، مؤجلة.الحالة، مؤجلة.النتيجة.القيمة)؛
    مؤجلة.ارفض(مثل_سندنا[سـندنا[خـطأ_عام]().{
        أنشئ()؛
        الرمز = 1؛
        الرسالة = نـص("خطأ غير معروف 2")؛
    }، خـطأ])؛
    طـرفية.اطبع("مؤجلة 2 - الحالة: %d، الخطأ: %s\ج"، مؤجلة.الحالة، مؤجلة.الخطأ.هات_الرسالة().صوان)؛
    مؤجلة.قرر(رقـم(5))؛
    طـرفية.اطبع("مؤجلة 2 - الحالة: %d، النتيجة: %d\ج"، مؤجلة.الحالة، مؤجلة.النتيجة.القيمة)؛
}
اختبر_مؤجلة()؛

// اختبر مـؤجلة.ثم
دالة اختبر_مؤجلة_ثم {
    طـرفية.اطبع("\جاختبر مـؤجلة.ثم:\ج")؛
    عرف مؤجلة: سـندنا[مـؤجلة[رقـم]] = مـؤجلة[رقـم].أنشئ()؛
    عرف ثم: سـندنا[مـؤجلة[نـص]] = مؤجلة.ثم[نـص](
        مغلفة (مدخل: رقـم، م: سند[مـؤجلة[نـص]]) {
            طـرفية.اطبع("أطلقت مـؤجلة.ثم\ج")؛
            م.قرر(نـص("مـؤجلة.ثم - النتيجة المستلمة: ") + مدخل.القيمة)؛
        }
    )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
    مؤجلة.قرر(رقـم(6))؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
}
اختبر_مؤجلة_ثم()؛

// اختبر مـؤجلة.اقبض
دالة اختبر_مؤجلة_اقبض {
    طـرفية.اطبع("\جاختبر مـؤجلة.اقبض:\ج")؛
    عرف مؤجلة: سـندنا[مـؤجلة[رقـم]] = مـؤجلة[رقـم].أنشئ()؛
    عرف اقبض: سـندنا[مـؤجلة[رقـم]] = مؤجلة.اقبض(
        مغلفة (خ: سـندنا[خـطأ]، م: سند[مـؤجلة[رقـم]]) {
            طـرفية.اطبع("أطلقت مـؤجلة.اقبض. الخطأ: %s\ج"، خ.هات_الرسالة().صوان)؛
            م.قرر(رقـم(7))؛
        }
    )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %d\ج"، اقبض.الحالة، اقبض.النتيجة.القيمة)؛
    مؤجلة.ارفض(مثل_سندنا[سـندنا[خـطأ_عام]().{
        أنشئ()؛
        الرمز = 1؛
        الرسالة = نـص("خطأ غير معروف 3")؛
    }، خـطأ])؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %d\ج"، اقبض.الحالة، اقبض.النتيجة.القيمة)؛
}
اختبر_مؤجلة_اقبض()؛

// اختبر مـؤجلة.ثم.اقبض
دالة اختبر_مؤجلة_ثم_اقبض {
    طـرفية.اطبع("\جاختبر مـؤجلة.ثم.اقبض:\ج")؛
    عرف مؤجلة: سـندنا[مـؤجلة[رقـم]] = مـؤجلة[رقـم].أنشئ()؛
    عرف اقبض: سـندنا[مـؤجلة[نـص]] = مؤجلة.ثم[نـص](
        مغلفة (مدخل: رقـم، م: سند[مـؤجلة[نـص]]) {
            طـرفية.اطبع("أطلقت مـؤجلة.ثم\ج")؛
            م.قرر(نـص("مـؤجلة.ثم - النتيجة المستلمة: ") + مدخل.القيمة)؛
        }
    ).اقبض(
        مغلفة (خ: سـندنا[خـطأ]، م: سند[مـؤجلة[نـص]]) {
            طـرفية.اطبع("أطلقت مـؤجلة.اقبض\ج")؛
            م.قرر(نـص("مـؤجلة.اقبض - الخطأ المستلم: ") + خ.هات_الرسالة())؛
        }
    )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، اقبض.الحالة، اقبض.النتيجة.صوان)؛
    مؤجلة.ارفض(مثل_سندنا[سـندنا[خـطأ_عام]().{
        أنشئ()؛
        الرمز = 1؛
        الرسالة = نـص("خطأ غير معروف 4")؛
    }، خـطأ])؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، اقبض.الحالة، اقبض.النتيجة.صوان)؛
}
اختبر_مؤجلة_ثم_اقبض()؛

// اختبر مـؤجلة.اقبض.ثم
دالة اختبر_مؤجلة_اقبض_ثم {
    طـرفية.اطبع("\جاختبر مـؤجلة.اقبض.ثم:\ج")؛
    عرف مؤجلة: سـندنا[مـؤجلة[رقـم]] = مـؤجلة[رقـم].أنشئ()؛
    عرف ثم: سـندنا[مـؤجلة[نـص]] = مؤجلة.اقبض(
        مغلفة (خ: سـندنا[خـطأ]، م: سند[مـؤجلة[رقـم]]) {
            طـرفية.اطبع("أطلقت مـؤجلة.اقبض\ج")؛
            م.قرر(رقـم(8))؛
        }
    ).ثم[نـص](
        مغلفة (مدخل: رقـم، م: سند[مـؤجلة[نـص]]) {
            طـرفية.اطبع("أطلقت مـؤجلة.ثم\ج")؛
            م.قرر(نـص("مـؤجلة.ثم - النتيجة المستلمة: ") + مدخل.القيمة)؛
        }
    )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
    مؤجلة.قرر(رقـم(9))؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
}
اختبر_مؤجلة_اقبض_ثم()؛

// اختبر مـؤجلة.الكل قرر.
دالة اختبر_مؤجلة_الكل_ثم {
    طـرفية.اطبع("\جاختبر مـؤجلة.الكل.ثم:\ج")؛
    عرف مؤجلة1: سـندنا[مـؤجلة[صـحيح]] = مـؤجلة[صـحيح].أنشئ()؛
    عرف مؤجلة2: سـندنا[مـؤجلة[صـحيح]] = مـؤجلة[صـحيح].أنشئ()؛
    عرف ثم: سـندنا[مـؤجلة[نـص]] = مـؤجلة[صـحيح]
        .الكل({ مؤجلة1، مؤجلة2 })
        .ثم[نـص](
            مغلفة (مدخل: مـصفوفة[صـحيح]، م: سند[مـؤجلة[نـص]]) {
                طـرفية.اطبع("أطلقت مـؤجلة.ثم\ج")؛
                م.قرر(نـص("مـؤجلة.ثم - النتيجة المستلمة: ") + مدخل(0) + "، " + مدخل(1))؛
            }
        )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
    مؤجلة1.قرر(10)؛
    طـرفية.اطبع("مؤجلة1 قررت. الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
    مؤجلة2.قرر(11)؛
    طـرفية.اطبع("مؤجلة2 قررت. الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
}
اختبر_مؤجلة_الكل_ثم()؛

// اختبر مـؤجلة.الكل ارفض.
دالة اختبر_مؤجلة_الكل_اقبض {
    طـرفية.اطبع("\جاختبر مـؤجلة.الكل.اقبض:\ج")؛
    عرف مؤجلة1: سـندنا[مـؤجلة[صـحيح]] = مـؤجلة[صـحيح].أنشئ()؛
    عرف مؤجلة2: سـندنا[مـؤجلة[صـحيح]] = مـؤجلة[صـحيح].أنشئ()؛
    عرف قبض: سـندنا[مـؤجلة[نـص]] = مـؤجلة[صـحيح]
        .الكل({ مؤجلة1، مؤجلة2 })
        .ثم[نـص](
            مغلفة (مدخل: مـصفوفة[صـحيح]، م: سند[مـؤجلة[نـص]]) {
                طـرفية.اطبع("أطلقت مـؤجلة.ثم\ج")؛
                م.قرر(نـص("مـؤجلة.ثم - النتيجة المستلمة: ") + مدخل(0) + "، " + مدخل(1))؛
            }
        ).اقبض(
            مغلفة (خ: سـندنا[خـطأ]، م: سند[مـؤجلة[نـص]]) {
                طـرفية.اطبع("أطلقت مـؤجلة.اقبض\ج")؛
                م.قرر(نـص("مـؤجلة.اقبض - الخطأ المستلم: ") + خ.هات_الرسالة())؛
            }
        )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، قبض.الحالة، قبض.النتيجة.صوان)؛
    مؤجلة1.قرر(10)؛
    طـرفية.اطبع("مؤجلة1 قررت. الحالة: %d، النتيجة: %s\ج"، قبض.الحالة، قبض.النتيجة.صوان)؛
    مؤجلة2.ارفض(مثل_سندنا[سـندنا[خـطأ_عام]().{
        أنشئ()؛
        الرمز = 1؛
        الرسالة = نـص("خطأ غير معروف 5")؛
    }، خـطأ])؛
    طـرفية.اطبع("مؤجلة2 رفضت. الحالة: %d، النتيجة: %s\ج"، قبض.الحالة، قبض.النتيجة.صوان)؛
}
اختبر_مؤجلة_الكل_اقبض()؛

دالة اختبر_تجاهل_النتيجة {
    طـرفية.اطبع("\جاختبر هذا.تجاهل_النتيجة:\ج")؛
    عرف مؤجلة1: سـندنا[مـؤجلة[رقـم]] = مـؤجلة[رقـم].أنشئ()؛
    عرف مؤجلة2: سـندنا[مـؤجلة[نـص]] = مـؤجلة[نـص].أنشئ()؛
    عرف ثم: سـندنا[مـؤجلة[نـص]] = مـؤجلة[صـحيح]
        .الكل({ مؤجلة1.تجاهل_النتيجة()، مؤجلة2.تجاهل_النتيجة() })
        .ثم[نـص](
            مغلفة (مدخل: مـصفوفة[صـحيح]، م: سند[مـؤجلة[نـص]]) {
                طـرفية.اطبع("أطلقت مـؤجلة.ثم\ج")؛
                م.قرر(نـص("مـؤجلة.ثم - النتيجة المستلمة: ") + مدخل(0) + "، " + مدخل(1))؛
            }
        )؛
    طـرفية.اطبع("الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
    مؤجلة1.قرر(رقـم(10))؛
    طـرفية.اطبع("مؤجلة1 قررت. الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
    مؤجلة2.قرر(نـص("11"))؛
    طـرفية.اطبع("مؤجلة2 قررت. الحالة: %d، النتيجة: %s\ج"، ثم.الحالة، ثم.النتيجة.صوان)؛
}
اختبر_تجاهل_النتيجة()؛

// TODO: Localize

